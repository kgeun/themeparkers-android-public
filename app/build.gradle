apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        applicationId "com.kgeun.themeparkers"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 2
        versionName "1.0.1"
        vectorDrawables.useSupportLibrary true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(THEMEPARKERS_STORE_FILE)
            storePassword THEMEPARKERS_STORE_PASSWORD
            keyAlias THEMEPARKERS_KEY_ALIAS
            keyPassword THEMEPARKERS_KEY_PASSWORD
        }
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            debuggable false

            buildConfigField 'boolean', 'LOG', 'false'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            debuggable true

            buildConfigField 'boolean', 'LOG', 'true'
        }
        applicationVariants.all { variant ->
            appendVersionName(variant, defaultConfig)
        }
    }

    flavorDimensions "default"
    productFlavors {
        live {
            dimension "default"
            buildConfigField 'String', 'APP_STATE', '"운영버전"'
            buildConfigField 'String', 'SERVER_URL', '"https://api.theme-parkers.com"'
            signingConfig signingConfigs.findByName("release")
            manifestPlaceholders = [appLabel: "@string/app_name", allowHttp: false]
        }
        qa {
            dimension "default"
            buildConfigField 'String', 'APP_STATE', '"통합테스트"'
            buildConfigField 'String', 'SERVER_URL', '"http://api-test.theme-parkers.com"'
            signingConfig signingConfigs.findByName("debug")
            manifestPlaceholders = [appLabel: "@string/qa_app_name", allowHttp: true]
            applicationIdSuffix '.qa'
        }
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

        // work-runtime-ktx 2.1.0 and above now requires Java 8
    kotlinOptions {
        jvmTarget = "1.8"
    }

}

def appendVersionName(variant, defaultConfig) {
    variant.outputs.all { output ->
        def date = new Date()
        def buildDate = date.format('yyyyMMdd')
        def buildType = variant.buildType.name
        def flavor = variant.productFlavors[0].name

        outputFileName = "Themeparkers_" + flavor + "_" + buildType + "_" + defaultConfig.versionName + "_" + defaultConfig.versionCode + "_" + buildDate + ".apk"
    }
}

dependencies {
    kapt "androidx.room:room-compiler:2.2.5"
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.core:core-ktx:1.3.1"
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation "androidx.room:room-runtime:2.2.5"
    implementation "androidx.room:room-ktx:2.2.5"
    implementation "androidx.work:work-runtime-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    implementation 'com.google.android.material:material:1.2.1'
    implementation "com.google.code.gson:gson:2.8.5"
    implementation 'com.flaviofaria:kenburnsview:1.0.7'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.17'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'

    implementation "org.koin:koin-androidx-scope:2.0.1"
    implementation "org.koin:koin-androidx-viewmodel:2.0.1"
    implementation 'org.koin:koin-core:2.0.1'
    implementation 'org.koin:koin-android:2.0.1'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation ("com.github.bumptech.glide:recyclerview-integration:4.11.0") {
        transitive = false
    }

    implementation 'com.github.ybq:Android-SpinKit:1.4.0'

    //joda, koda-time
    implementation 'joda-time:joda-time:2.10.6'
    implementation 'com.github.debop:koda-time:1.2.1'

    implementation 'org.jsoup:jsoup:1.13.1'

    implementation "androidx.browser:browser:1.2.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    implementation "org.unbescape:unbescape:1.1.6.RELEASE"

    implementation ('com.github.abdularis:parallaximageview:1.1'){
        exclude group: "org.jetbrains", module: 'kotlin-stdlib-jdk7'
    }

    testImplementation 'org.koin:koin-test:2.0.0-rc-2'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8"

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:1.0.1"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"
    implementation "com.google.android.material:material:1.2.1"

    implementation 'com.github.Jay-Goo:RangeSeekBar:3.0.0'
    implementation 'com.otaliastudios:zoomlayout:1.8.0'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.github.addisonelliott:SegmentedButton:3.1.9'

    implementation platform('com.google.firebase:firebase-bom:25.12.0')

    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-crashlytics'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}